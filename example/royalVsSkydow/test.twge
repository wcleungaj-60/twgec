__skydowLocs__ = [(7,1)];
__royalLocs__ = [(22,18)];
__thirdLocs__ = [(28,1),(1,28)];
__title__ = "皇家天影之爭";
__useDefaultItems__ = true;
__map__ = "CG.TWEventsGameTemplate/gamemap.twmap";

alias addSniper(camp, role, x, y) {
    addActor(name = "狙擊手(200)", id = "sniper*", camp = camp, hp = 200, x = x, y = y, range = 0, weapon1 = "盾牌", weapon2 = "狙擊槍", role = role);
}

alias addCamp(camp, id, x, y) {
    addActor(name = "軍營(500)", id = id, camp = camp, hp = 500, x = x, y = y, range = 0, weapon1 = "拳頭", weapon2 = "拳頭", externRole = "TWilightGameEventCompiler.character_camp");
}

alias setInt(key, value) {
    setGlobal(key = key, type = "integer", value = value);
}

alias setNum(key, value) {
    setGlobal(key = key, type = "number", value = value);
}

alias addJobMapSign(x, y, rotation, text, buttonCodeSuffix) {
    addMapSign(x = x, y = y, rotation = rotation, text = text, buttonCode = "chooseJob" + buttonCodeSuffix, buttonLabel = "選擇");
}

alias chooseJob(jobName) {
    setObjectVar(object = "instance", key = "job", type = "string", value = jobName);
    actorTalk(actorId = "instance", text = "已選擇職業：{instance.job}");
}

block init {
    actions {
        setInt("royalHp", 100);
        setInt("skydowHp", 100);
        addSniper("天影", "天影武士1", 11, 7);
        addSniper("皇家", "皇家機械人", 18, 12);
        addCamp("天影", "camp_skydow", 9, 6);
        addCamp("皇家", "camp_royal", 20, 13);
        addJobMapSign(27, 17, 0, "【守望者】\n使用防護罩時，恢復30點體力", "R1");
        addJobMapSign(27, 16, 0, "【新兵訓練師】\n我方士兵血量上限加10", "R2");
        addJobMapSign(2, 2, 180, "【龍騎士】\n使用黑龍波時，會額外召喚出一條黑龍波", "S1");
        addJobMapSign(2, 3, 180, "【忍者】\n使用影分身時，會召喚一個持續10秒的100血分身", "S2");
    }
}

block spawn {
    __delay__ = 5000;
    __repeat__ = 9999;
    __repeatInterval__ = 10000;
    actions {
        addActor(name = "天影士兵({skydowHp})", camp = "天影", hp = "{skydowHp}", x = 1, y = 1, role = "天影士兵", patrol = [(14,1),(15,18)]);
        addActor(name = "皇家士兵({royalHp})", camp = "皇家", hp = "{royalHp}", x = 28, y = 18, role = "皇家士兵", patrol = [(15,18),(14,1)]);
        setInt("skydowHp", "{skydowHp} + 1");
        setInt("royalHp", "{royalHp} + 1");
    }
}

block sniperAttack {
    __repeat__ = 9999;
    triggers {
        actorFire(varName = "instance", actorId = "sniper");
    }
    actions {
        setNum("angle", "{instance.bodyRotation}*180/3.14");
        enblastEffect(fromActor = "{instance}", toAngle = "{angle}");
    }
}

block chooseJobR1 {
    __repeat__ = 9999;
    triggers {
        clickButton(varName = "instance", buttonId = "chooseJobR1");
    }
    checks {
        checkString(value = "{instance.job}", matchKind = "equal", str = "undefined");
    }
    actions {
        chooseJob("守望者");
    }
}

block chooseJobR2 {
    __repeat__ = 9999;
    triggers {
        clickButton(varName = "instance", buttonId = "chooseJobR2");
    }
    checks {
        checkString(value = "{instance.job}", matchKind = "equal", str = "undefined");
    }
    actions {
        chooseJob("新兵訓練師");
        setInt("royalHp", "{royalHp} + 10");
    }
}

block chooseJobS1 {
    __repeat__ = 9999;
    triggers {
        clickButton(varName = "instance", buttonId = "chooseJobS1");
    }
    checks {
        checkString(value = "{instance.job}", matchKind = "equal", str = "undefined");
    }
    actions {
        chooseJob("龍騎士");
    }
}

block chooseJobS2 {
    __repeat__ = 9999;
    triggers {
        clickButton(varName = "instance", buttonId = "chooseJobS2");
    }
    checks {
        checkString(value = "{instance.job}", matchKind = "equal", str = "undefined");
    }
    actions {
        chooseJob("忍者");
    }
}

block releasePowerR1 {
    __repeat__ = 9999;
    triggers {
        releasePower(varName = "instance", ability = "防護罩");
    }
    checks {
        checkString(value = "{instance.job}", matchKind = "equal", str = "守望者");
    }
    actions {
        deltaHp(actorCode = "instance", value = 30);
    }
}

block releasePowerS1 {
    __repeat__ = 9999;
    triggers {
        releasePower(varName = "instance", weapon = "大刀");
    }
    checks {
        checkString(value = "{instance.job}", matchKind = "equal", str = "龍騎士");
    }
    actions {
        longBo(actorCode = "instance");
    }
}

block releasePowerS2 {
    __repeat__ = 9999;
    triggers {
        releasePower(varName = "instance", ability = "分身術");
    }
    checks {
        checkString(value = "{instance.job}", matchKind = "equal", str = "忍者");
    }
    actions {
        setNum("tempX", "{instance.x}/32");
        setNum("tempY", "{instance.y}/32");
        addActor(name = "分身", localVarname = "actor", camp = "天影", hp = 100, x = "{tempX}", y = "{tempY}", weapon1 = "爪子", weapon2 = "步槍", role = "天影武士1");
        wait(duration = 10000);
        actorDisappear(actorId = "actor");
    }
}

block royalWin {
    triggers {
        actorDead(actorId = "camp_skydow");
    }
    actions {
        missionComplete(camp = "royal");
    }
}

block skydowWin {
    triggers {
        actorDead(actorId = "camp_royal");
    }
    actions {
        missionComplete(camp = "skydow");
    }
}