// Metadata Setting
__skydowLocs__ = [Point(23,26)];
__royalLocs__ = [Point(23,26)];
__thirdLocs__ = [Point(23,26)];
__lives__ = 9999;
__title__ = "RPG試驗";
__useDefaultItems__ = true;
__map__ = "CG.TWilightGameEventCompiler/rpg/simple_rpg.twmap";
__roomSize__ = 1;

// Constant Definition
const INSTANCE_ANGLE = "{{instance.id}.bodyRotation}*180/3.14";
const STATUS_TEXT = "【Lv{Level}】\nExp:{Exp}/{ExpNeeded}\n尚餘能力點：{Point}\n力量：{STR}\n體質：{CON}\n敏捷：{AGI}\n智慧：{INT}\n精神：{PSY}";

// Utils Function Definition
def setInt(key, value) : actions {
    setGlobal(key = key, type = "integer", value = value);
}

def setNum(key, value) : actions {
    setGlobal(key = key, type = "number", value = value);
}

def setString(key, value) : actions {
    setGlobal(key = key, type = "string", value = value);
}

def increment(key) : actions {
    setInt(key = key, value = "{"+key+"}+1");
}

def decrement(key) : actions {
    setInt(key = key, value = "{"+key+"}-1");
}

// Block Function Definition
def clickIncrease(type, typeZh) : block {
    __repeat__ = -1;
    triggers {
        clickButton(varName = "instance", buttonId = "increase" + type);
    }
    checks {
        checkNumber(lhs = "{Point}", op = ">", rhs = 0);
    }
    actions {
        decrement("Point");
        increment(type);
        actorTalk(actorId = "instance", text = "成功提升一點"+typeZh+"，尚餘{Point}點能力點");
        if(type == "CON") {
            actorAttributes(actorId = "instance", attr = "maxHp", value = "100+10*{CON}");
            deltaHp(actorCode = "instance", value = "100+10*{CON}");
        }
        if(type == "AGI") {
            // when AGI = 0, closeWeaponSpeed = 1
            // when AGI = 1, closeWeaponSpeed = 1.1429
            // when AGI = 5, closeWeaponSpeed = 1.6000
            // when AGI = 25, closeWeaponSpeed = 2.6667
            // when AGI = 125, closeWeaponSpeed = 3.5862
            // when AGI = inf, closeWeaponSpeed = 4
            actorAttributes(actorId = "instance", attr = "closeWeaponSpeed", value = "4-3/(1+0.05*{AGI})");
            actorAttributes(actorId = "instance", attr = "weight", value = "3*pow(0.97,{AGI})");
        }
    }
}

block clickIncreaseSTR = clickIncrease("STR", "力量");
block clickIncreaseCON = clickIncrease("CON", "體質");
block clickIncreaseAGI = clickIncrease("AGI", "敏捷");
block clickIncreaseINT = clickIncrease("INT", "智慧");
block clickIncreasePSY = clickIncrease("PSY", "精神");

// Block Implementation
block monsterDead {
    __repeat__ = -1;
    triggers {
        actorDead(actor = ActorMatch(id = "monster"));
    }
    actions {
        increment("Exp");
        // TODO: Popup text
    }
}

block monsterSpawn {
    __repeat__ = -1;
    __repeatInterval__ = 15000;
    checks {
        //TODO: Support Check number of character
    }
    actions {
        addActor(name = "鹿(60)", id = "monster*", camp = "第三", 
                hp = 60, x = "random(5,25)", y = "random(5,25)", localVarname = "actor", 
                weapon1 = "fist", weapon2 = "fist", strength = "10", role = "鹿");
        actorFollow(actorId = "actor", type = "跟隨自己");
    }
}

block levelUp {
    __repeat__ = -1;
    checks {
        checkNumber(lhs = "{Exp}", op = ">=", rhs = "{ExpNeeded}");
    }
    actions {
        setInt("Exp", "{Exp}-{ExpNeeded}");
        increment("Level");
        increment("Point");
        increment("Point");
        setInt("ExpNeeded", "{Level}*5+5");
    }
}

block recover {
    __repeat__ = -1;
    __repeatInterval__ = 10000;
    checks {
        forEachActor(actor = ActorMatch(controller = "玩家"), varname = "instance");
        checkNumber(lhs = "{PSY}", op = ">", rhs = 0);
    }
    actions {
        deltaHp(actorCode = "instance", value = "{PSY}");
    }
}

block pressZ {
    __repeat__ = -1;
    triggers {
        keyboardPressed(varName = "instance", key = "Z");
    }
    actions {
        actorTalk(actorId = "{instance.id}", text = STATUS_TEXT);
    }
}

block pressX {
    __repeat__ = -1;
    __repeatInterval__ = 3000;
    triggers {
        keyboardPressed(varName = "instance", key = "X");
    }
    actions {
        enblastEffect(fromActor = "{instance.id}", toAngle = INSTANCE_ANGLE, scale = "0.5", damage = "{INT} + 1");
        enblastEffect(fromActor = "{instance.id}", toAngle = INSTANCE_ANGLE + "+20", scale = "0.5", damage = "{INT} + 1");
        enblastEffect(fromActor = "{instance.id}", toAngle = INSTANCE_ANGLE + "-20", scale = "0.5", damage = "{INT} + 1");
        wait(duration = 3000);
        actorTalk(actorId = "{instance.id}", text = "光彈冷卻完畢");
    }
}

block init {
    actions {
        setInt("STR", 0);
        setInt("CON", 0);
        setInt("AGI", 0);
        setInt("INT", 0);
        setInt("PSY", 0);
        setInt("Level", 1);
        setInt("Point", 5);
        setInt("Exp", 0);
        setInt("ExpNeeded", 10);
        addMapSign(x = 19, y = 28, rotation = 270, text = "【力量】\n消耗一點能力點，增加小刀一點傷害 (未實現)", buttonCode = "increaseSTR", buttonLabel = "確認");
        addMapSign(x = 21, y = 28, rotation = 270, text = "【體質】\n消耗一點能力點，增加十點血量上限", buttonCode = "increaseCON", buttonLabel = "確認");
        addMapSign(x = 23, y = 28, rotation = 270, text = "【敏捷】\n消耗一點能力點，重量減少3%，並提升近戰武器速度", buttonCode = "increaseAGI", buttonLabel = "確認");
        addMapSign(x = 25, y = 28, rotation = 270, text = "【智慧】\n消耗一點能力點，增加按X技能的光彈一點傷害", buttonCode = "increaseINT", buttonLabel = "確認");
        addMapSign(x = 27, y = 28, rotation = 270, text = "【精神】\n消耗一點能力點，每十秒恢復一點體力", buttonCode = "increasePSY", buttonLabel = "確認");
    }
}