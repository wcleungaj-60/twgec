//------------------- 自定義常量 -------------------//
const 公式::Instance角度 = "{instance.bodyRotation}*180/3.14";
const 公式::Instance血量 = "98 + 10*{instance.CON} + 2*{instance.Level}";
const 公式::Instance恢復量 = "1 + {instance.PSY} + {instance.CON}/2";
const 公式::風杖傷害 = "{instance.INT} + {instance.PSY}/2 + 10";
const 公式::鐵劍技能恢復 = "1 + {instance.CON}";
const 公式::鐵劍技能傷害 = "1 + {instance.STR}/2 + {instance.CON}/2";

const 文字::Instance訊息 = 
"【Lv{{instance.id}.Level}】\n" +
"Exp:{{instance.id}.Exp}/{{instance.id}.ExpNeeded}\n" +
"尚餘能力點：{{instance.id}.Point}\n" +
"力量：{{instance.id}.STR}\n" +
"體質：{{instance.id}.CON}\n" +
"敏捷：{{instance.id}.AGI}\n" +
"智慧：{{instance.id}.INT}\n" +
"精神：{{instance.id}.PSY}";

const 出生點 = Point(10,10);
const 除錯模式 = false;
const 狀態讀寫密鑰 = "simple-rpg-test";

//------------------- Metadata設定 -------------------//
__skydowLocs__ = 出生點;
__royalLocs__ = 出生點;
__thirdLocs__ = 出生點;
__lives__ = 9999;
__title__ = "RPG試驗";
__useDefaultItems__ = true;
__useCustomWeapons__ = true;
__customWeapons__ = [
    CustomWeapon(reference = "TWilightGameEventCompiler.weapon_wind_staff", code = "wind_staff", damage = 5, fireType = "單手橫劈",
    pivotOnHandDegree = 58, pivotOnHandX = 2, pivotOnHandY = 9, pivotOnHandXScale = "1.3", pivotOnHandYScale = "1.5",
    pivotOnIconDegree = -40, pivotOnIconX = 20, pivotOnIconY = 20),
    CustomWeapon(reference = "TWilightGameEventCompiler.weapon_iron_sword", code = "iron_sword", damage = 30, fireType = "大刀砍劈",
    pivotOnHandDegree = 35, pivotOnHandX = -12,
    pivotOnIconDegree = -40, pivotOnIconX = 27, pivotOnIconY = 14)
];
__map__ = "CG.TWilightGameEventCompiler/rpg/simple_rpg.twmap";
__roomSize__ = 8;

//------------------- 自定義功能 -------------------//
def 自定義::除錯(text): actions {
    if(除錯模式) {
        print(type = "error", text = text);
    }
}

def 自定義::儲存Instance整數(key, value): actions {
    儲存物件變數(key = key, object = "instance", type = "integer", value = value);
}

def 自定義::設定雙向地圖傳送點(x1, y1, x2, y2): actions {
    if (x1 == x2 && y1 < y2) {
        設定地圖傳送點(fromX = x1, fromY = y1, toX = x2, toY = y2, direction = "下");
        設定地圖傳送點(fromX = x2, fromY = y2, toX = x1, toY = y1, direction = "上");
    } else if (x1 == x2 && y1 > y2) {
        設定地圖傳送點(fromX = x1, fromY = y1, toX = x2, toY = y2, direction = "上");
        設定地圖傳送點(fromX = x2, fromY = y2, toX = x1, toY = y1, direction = "下");
    } else if (x1 > x2 && y1 == y2) {
        設定地圖傳送點(fromX = x1, fromY = y1, toX = x2, toY = y2, direction = "左");
        設定地圖傳送點(fromX = x2, fromY = y2, toX = x1, toY = y1, direction = "右");
    } else if (x1 < x2 && y1 == y2) {
        設定地圖傳送點(fromX = x1, fromY = y1, toX = x2, toY = y2, direction = "右");
        設定地圖傳送點(fromX = x2, fromY = y2, toX = x1, toY = y1, direction = "左");
    } else if (x1 == x2 && y1 == y2) {
        print(type = "error", text = "Cannot set the same point as the mapWarp src and dst");
    } else {
        print(type = "error", text = "Bidirectional mapWarp requires x or y to be the same value");
    }
}

def 自定義::更新角色屬性(actorId): actions {
    儲存物件變數(key = "HP", object = actorId, type = "integer", value = "98 + 10*{"+actorId+".CON} + 2*{"+actorId+".Level}");
    設定角色屬性(actorId = actorId, attr = "maxHp", value = "{"+actorId+".HP}");
    設定角色屬性(actorId = actorId, attr = "closeWeaponSpeed", value = "4-3/(1+0.05*{"+actorId+".AGI})");
    設定角色屬性(actorId = actorId, attr = "weight", value = "3*pow(0.97,{"+actorId+".AGI})");
}

def 自定義::輔助::下載Instance數據(key) : actions {
    取得玩家狀態(category = 狀態讀寫密鑰, key = key, playerId = "{instance}", varName = "{instance}_"+key);
    自定義::儲存Instance整數(key, "{{instance}_"+key+"}");
}

def 自定義::輔助::上載Instance數據(key) : actions {
    儲存玩家狀態(category = 狀態讀寫密鑰, key = key, playerId = "{instance}", type = "string", value = "{{instance}."+key+"}");
}

def 自定義::上載Instance數據() : actions {
    自定義::輔助::上載Instance數據("Point");
    自定義::輔助::上載Instance數據("ExpNeeded");
    自定義::輔助::上載Instance數據("Level");
    自定義::輔助::上載Instance數據("STR");
    自定義::輔助::上載Instance數據("CON");
    自定義::輔助::上載Instance數據("AGI");
    自定義::輔助::上載Instance數據("INT");
    自定義::輔助::上載Instance數據("PSY");
    自定義::輔助::上載Instance數據("Exp");
    自定義::輔助::上載Instance數據("HP");
    儲存玩家狀態(category = 狀態讀寫密鑰, key = "savedBefore", playerId = "{instance}", type = "integer", value = "1");
}

//------------------- 自定義事件泛型 -------------------//

def 自定義::下載玩家數據泛型(hasSavedBefore) : block {
    __repeat__ = -1;
    checks {
        找出所有角色(actor = ActorMatch(controller = "player"), varname = "instance");
        比較數字(lhs = "{instance.completedInit}", op = "==", rhs = 1);
        if(hasSavedBefore) {
            比對字串(str = "{{instance}_savedBefore}", matchKind = "equal", value = "1");
        }
        if(hasSavedBefore == false) {
            比對字串(str = "{{instance}_savedBefore}", matchKind = "equal", value = "undefined");
        } 
    }
    actions {
        儲存物件變數(key = "completedInit", object = "instance", type = "string", value = 2);
        if(hasSavedBefore) {
            自定義::輔助::下載Instance數據("Point");
            自定義::輔助::下載Instance數據("ExpNeeded");
            自定義::輔助::下載Instance數據("Level");
            自定義::輔助::下載Instance數據("STR");
            自定義::輔助::下載Instance數據("CON");
            自定義::輔助::下載Instance數據("AGI");
            自定義::輔助::下載Instance數據("INT");
            自定義::輔助::下載Instance數據("PSY");
            自定義::輔助::下載Instance數據("Exp");
            自定義::輔助::下載Instance數據("HP");
            自定義::更新角色屬性("instance");
            角色加減血(actorCode = "instance", type = "heal", value = 9999);
        }
    }
}

block 下載老玩家數據 = 自定義::下載玩家數據泛型(hasSavedBefore = true);
block 下載新玩家數據 = 自定義::下載玩家數據泛型(hasSavedBefore = false);

def 自定義::點擊提升能力泛型(type, typeZh) : block {
    __repeat__ = -1;
    triggers {
        告示牌按鈕(varName = "instance", buttonId = "increase" + type);
    }
    checks {
        比較數字(lhs = "{instance.Point}", op = ">", rhs = 0);
    }
    actions {
        自定義::儲存Instance整數("Point", "{instance.Point}-1");
        自定義::儲存Instance整數(type, "{instance."+type+"}+1");
        角色說話(actorId = "instance", text = "成功提升一點" + typeZh + "，尚餘{instance.Point}點能力點");
        自定義::更新角色屬性("instance");
        自定義::上載Instance數據();
    }
}

block 點擊提升力量 = 自定義::點擊提升能力泛型("STR", "力量");
block 點擊提升體質 = 自定義::點擊提升能力泛型("CON", "體質");
block 點擊提升敏捷 = 自定義::點擊提升能力泛型("AGI", "敏捷");
block 點擊提升智慧 = 自定義::點擊提升能力泛型("INT", "智慧");
block 點擊提升精神 = 自定義::點擊提升能力泛型("PSY", "精神");

def 自定義::釋放技能泛型(weapon) : block {
    __repeat__ = -1;
    triggers {
        發動技能(varName = "instance", weapon = weapon);
    }
    actions {
        if(weapon == "custom_wind_staff") {
        光彈特效(fromActor = "instance", scale = "0.5", damage = 公式::風杖傷害, toAngle = 公式::Instance角度 + 32);
        光彈特效(fromActor = "instance", scale = "0.5", damage = 公式::風杖傷害, toAngle = 公式::Instance角度 + 16);
        光彈特效(fromActor = "instance", scale = "0.5", damage = 公式::風杖傷害, toAngle = 公式::Instance角度);
        光彈特效(fromActor = "instance", scale = "0.5", damage = 公式::風杖傷害, toAngle = 公式::Instance角度 - 16);
        光彈特效(fromActor = "instance", scale = "0.5", damage = 公式::風杖傷害, toAngle = 公式::Instance角度 - 32);
        }
        if(weapon == "custom_iron_sword") {
            EnhFF::廣義圓形範圍(actorId = "instance", radius = 3*32, duration = 10000,
                color = "#D16500", deltaHpType = "heal", deltaHpValue = 公式::鐵劍技能恢復,
                deltaHpCD = 4000, deltaHpTarget = ActorMatch(controller = "player"), lineWidth = 2);
            EnhFF::廣義圓形範圍(actorId = "instance", radius = 3*32, duration = 10000,
                color = "#D16500", deltaHpType = "damage", deltaHpValue = 公式::鐵劍技能傷害,
                deltaHpCD = 4000, deltaHpTarget = ActorMatch(controller = "ai"), lineWidth = 2);
        }
    }
}

block 釋放技能風杖 = 自定義::釋放技能泛型("custom_wind_staff");
block 釋放技能鐵劍 = 自定義::釋放技能泛型("custom_iron_sword");

//------------------- 遊戲事件 -------------------//

block 點擊重置能力 {
    __repeat__ = -1;
    triggers {
        告示牌按鈕(varName = "instance", buttonId = "ResetPoint");
    }
    actions {
        自定義::儲存Instance整數("Point", "{instance.Level}*2+3");
        自定義::儲存Instance整數("STR", 0);
        自定義::儲存Instance整數("CON", 0);
        自定義::儲存Instance整數("AGI", 0);
        自定義::儲存Instance整數("INT", 0);
        自定義::儲存Instance整數("PSY", 0);
        角色說話(actorId = "instance", text = "成功重設能力點，目前有{instance.Point}點能力點");
        自定義::更新角色屬性("instance");
        自定義::上載Instance數據();
    }
}

block 提升經驗 {
    __repeat__ = -1;
    triggers {
        拾取武器道具(actorVarname = "instance", matchKind = "equal", itemVarname = "deer_exp");
    }
    actions {
        自定義::儲存Instance整數("Exp", "{instance.Exp}"+1);
        // TODO: Popup text
        // TODO: Call event
        自定義::上載Instance數據();
    }
}

block 野獸死亡 {
    __repeat__ = -1;
    triggers {
        角色死亡(actor = ActorMatch(id = "monster"), varName = "instance");
    }
    actions {
        新增放置可拾取道具(itemCode = "deer_exp", x = "({instance.gamePosition.x}-16)/32", y = "({instance.gamePosition.y}-16)/32", type = "sariraFire");
    }
}

block 野獸重生 {
    __repeat__ = -1;
    __repeatInterval__ = 8000;
    checks {
        計算人數(actor = ActorMatch(id = "monster_deer", matchKind = "contain"), op = "<", value = "5");
    }
    actions {
        新增角色(name = "鹿(60)", id = "monster_deer*", camp = "第三", 
                hp = 60, x = "random(21,40)", y = "random(0,19)", localVarname = "actor", 
                weapon1 = "fist", weapon2 = "fist", strength = "10", role = "鹿");
        跟隨人物(actorId = "actor", type = "跟隨自己");
    }
}

block 等級提升 {
    __repeat__ = -1;
    checks {
        找出所有角色(actor = ActorMatch(controller = "玩家"), varname = "instance");
        比較數字(lhs = "{instance.Exp}", op = ">=", rhs = "{instance.ExpNeeded}");
    }
    actions {
        自定義::儲存Instance整數("Exp", "{instance.Exp}-{instance.ExpNeeded}");
        自定義::儲存Instance整數("Level", "{instance.Level}"+1);
        自定義::儲存Instance整數("Point", "{instance.Point}"+2);
        自定義::儲存Instance整數("ExpNeeded", "{instance.Level}*2+4");
        自定義::儲存Instance整數("HP", 公式::Instance血量);
        設定角色屬性(actorId = "instance", attr = "maxHp", value = "{instance.HP}");
        自定義::上載Instance數據();
    }
}

block 回血 {
    __repeat__ = -1;
    __repeatInterval__ = 10000;
    checks {
        找出所有角色(actor = ActorMatch(controller = "玩家"), varname = "instance");
    }
    actions {
        角色加減血(actorCode = "instance", value = 公式::Instance恢復量);
    }
}

block 查看面板 {
    __repeat__ = -1;
    triggers {
        鍵盤按鍵(varName = "instance", key = "Z");
    }
    actions {
        角色說話(actorId = "{instance.id}", text = 文字::Instance訊息);
    }
}

block 法杖攻擊 {
    __repeat__ = -1;
    triggers {
        角色發動攻擊(weapon = "custom_wind_staff", varName = "instance");
    }
    actions {
        光彈特效(fromActor = "instance", toAngle = 公式::Instance角度, scale = "0.5", damage = 公式::風杖傷害);
    }
}

block 鐵劍攻擊 {
    __repeat__ = -1;
    triggers {
        角色受傷(actor = ActorMatch(), actorVarName = "instance", hitter = ActorMatch(), hitterVarName = "attacker", weapon = "custom_iron_sword");
    }
    actions {
        角色加減血(actorCode = "instance", casterCode = "attacker", type = "流血受傷", value = "{attacker.STR}*3");
    }
}

block 玩家初始化 {
    __repeat__ = -1;
    triggers {
        角色進入戰場(actor = ActorMatch(controller = "player"), varName = "instance");
    }
    actions {
        自定義::儲存Instance整數("STR", 0);
        自定義::儲存Instance整數("CON", 0);
        自定義::儲存Instance整數("AGI", 0);
        自定義::儲存Instance整數("INT", 0);
        自定義::儲存Instance整數("PSY", 0);
        自定義::儲存Instance整數("Level", 1);
        自定義::儲存Instance整數("Point", 5);
        自定義::儲存Instance整數("Exp", 0);
        自定義::儲存Instance整數("ExpNeeded", 6);
        自定義::儲存Instance整數("HP", 100);
        取得玩家狀態(category = 狀態讀寫密鑰, key = "savedBefore", playerId = "{instance}", varName = "{instance}_savedBefore");
        自定義::儲存Instance整數("completedInit", 1);
    }
}

block 遊戲初始化 {
    actions {
        設定武器技能(weapon = "小刀", operation = "無技能", level = 1);
        設定武器技能(weapon = "小刀", operation = "無技能", level = 2);
        設定武器技能(weapon = "custom_wind_staff");
        設定武器技能(weapon = "custom_iron_sword");
        新增武器道具(x = 4, y = 8, item = "custom_wind_staff");
        新增武器道具(x = 4, y = 8, item = "custom_wind_staff");
        新增武器道具(x = 4, y = 8, item = "custom_wind_staff");
        新增武器道具(x = 4, y = 8, item = "custom_wind_staff");
        新增武器道具(x = 4, y = 8, item = "custom_wind_staff");
        新增告示牌(x = 4, y = 7, rotation = 90, text = "【法杖】\n武器傷害：5\n - 攻擊附帶傷害為【10+智慧+精神/2】的光彈\n技能：釋出5發光彈", showButtons = false);
        新增告示牌(x = 6, y = 7, rotation = 90, text = "【鐵劍】\n武器傷害：【30+力量*3】\n技能：範圍造成敵人【1+力量/2+體質/2】的傷害3次，並範圍恢復玩家【1+體質】的血量3次", showButtons = false);
        自定義::設定雙向地圖傳送點(x1 = 19, y1 = 9, x2 = 21, y2 = 9);
        自定義::設定雙向地圖傳送點(x1 = 19, y1 = 10, x2 = 21, y2 = 10);
        新增武器道具(x = 6, y = 8, item = "custom_iron_sword");
        新增武器道具(x = 6, y = 8, item = "custom_iron_sword");
        新增武器道具(x = 6, y = 8, item = "custom_iron_sword");
        新增武器道具(x = 6, y = 8, item = "custom_iron_sword");
        新增武器道具(x = 6, y = 8, item = "custom_iron_sword");
        新增告示牌(x = 3, y = 17, rotation = 270, text = "【力量】\n消耗一點能力點，提升劍類傷害", buttonCode = "increaseSTR", buttonLabel = "確認");
        新增告示牌(x = 5, y = 17, rotation = 270, text = "【體質】\n消耗一點能力點，增加十點血量上限及半點恢復力", buttonCode = "increaseCON", buttonLabel = "確認");
        新增告示牌(x = 7, y = 17, rotation = 270, text = "【敏捷】\n消耗一點能力點，重量減少3%，並提升武器攻擊速度", buttonCode = "increaseAGI", buttonLabel = "確認");
        新增告示牌(x = 9, y = 17, rotation = 270, text = "【智慧】\n消耗一點能力點，提升杖類傷害", buttonCode = "increaseINT", buttonLabel = "確認");
        新增告示牌(x = 11, y = 17, rotation = 270, text = "【精神】\n消耗一點能力點，小量提升杖類傷害及提升一點恢復力", buttonCode = "increasePSY", buttonLabel = "確認");
        新增告示牌(x = 1, y = 15, rotation = 0, text = "【重置能力點分配】", buttonCode = "ResetPoint", buttonLabel = "確認");
    }
}