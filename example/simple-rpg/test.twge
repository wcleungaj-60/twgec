// Constant Definition
const INSTANCE_ANGLE = "{instance.bodyRotation}*180/3.14";
const WIND_STAFF_ENBLAST_DAMAGE = "{INT} + {PSY}/2 + 10";
const HP_FORMULA = "100 + 10*{CON} + 2*{Lv}";
const RECOVERY_FORMULA = "1 + {PSY} + {CON}/2";
const STATUS_TEXT = 
"【Lv{Level}】\n" +
"Exp:{Exp}/{ExpNeeded}\n" +
"尚餘能力點：{Point}\n" +
"力量：{STR}\n" +
"體質：{CON}\n" +
"敏捷：{AGI}\n" +
"智慧：{INT}\n" +
"精神：{PSY}";
const SPAWN_POINT = Point(10,10);

// Metadata Setting
__skydowLocs__ = SPAWN_POINT;
__royalLocs__ = SPAWN_POINT;
__thirdLocs__ = SPAWN_POINT;
__lives__ = 9999;
__title__ = "RPG試驗";
__useDefaultItems__ = true;
__useCustomWeapons__ = true;
__customWeapons__ = [
    CustomWeapon(reference = "TWilightGameEventCompiler.weapon_wind_staff", code = "wind_staff", damage = 5, fireType = "單手橫劈",
    pivotOnHandDegree = 58, pivotOnHandX = 2, pivotOnHandY = 9, pivotOnHandXScale = "1.3", pivotOnHandYScale = "1.5",
    pivotOnIconDegree = -40, pivotOnIconX = 20, pivotOnIconY = 20),
    CustomWeapon(reference = "TWilightGameEventCompiler.weapon_iron_sword", code = "iron_sword", damage = 30, fireType = "大刀砍劈",
    pivotOnHandDegree = 35, pivotOnHandX = -12,
    pivotOnIconDegree = -40, pivotOnIconX = 27, pivotOnIconY = 14)
];
__map__ = "CG.TWilightGameEventCompiler/rpg/simple_rpg.twmap";
__roomSize__ = 1;

// Utils Function Definition
def setInt(key, value) : actions {
    setGlobal(key = key, type = "integer", value = value);
}

def setNum(key, value) : actions {
    setGlobal(key = key, type = "number", value = value);
}

def setString(key, value) : actions {
    setGlobal(key = key, type = "string", value = value);
}

def increment(key) : actions {
    setInt(key = key, value = "{"+key+"}+1");
}

def decrement(key) : actions {
    setInt(key = key, value = "{"+key+"}-1");
}

// Block Function Definition
def clickIncreaseDef(type, typeZh) : block {
    __repeat__ = -1;
    triggers {
        clickButton(varName = "instance", buttonId = "increase" + type);
    }
    checks {
        checkNumber(lhs = "{Point}", op = ">", rhs = 0);
    }
    actions {
        decrement("Point");
        increment(type);
        actorTalk(actorId = "instance", text = "成功提升一點" + typeZh + "，尚餘{Point}點能力點");
        if(type == "CON") {
            setInt("HP", HP_FORMULA);
            actorAttributes(actorId = "instance", attr = "maxHp", value = "{HP}");
        }
        if(type == "AGI") {
            // when AGI = 0, closeWeaponSpeed = 1
            // when AGI = 1, closeWeaponSpeed = 1.1429
            // when AGI = 5, closeWeaponSpeed = 1.6000
            // when AGI = 25, closeWeaponSpeed = 2.6667
            // when AGI = 125, closeWeaponSpeed = 3.5862
            // when AGI = inf, closeWeaponSpeed = 4
            actorAttributes(actorId = "instance", attr = "closeWeaponSpeed", value = "4-3/(1+0.05*{AGI})");
            actorAttributes(actorId = "instance", attr = "weight", value = "3*pow(0.97,{AGI})");
        }
    }
}

def releasePowerDef(weapon) : block {
    __repeat__ = -1;
    triggers {
        releasePower(varName = "instance", weapon = weapon);
    }
    actions {
        if(weapon == "custom_wind_staff") {
        // TODO: Implicit Conversion from Int to String
        enblastEffect(fromActor = "instance", scale = "0.5", damage = WIND_STAFF_ENBLAST_DAMAGE,  toAngle = INSTANCE_ANGLE + "+32");
        enblastEffect(fromActor = "instance", scale = "0.5", damage = WIND_STAFF_ENBLAST_DAMAGE,  toAngle = INSTANCE_ANGLE + "+16");
        enblastEffect(fromActor = "instance", scale = "0.5", damage = WIND_STAFF_ENBLAST_DAMAGE,  toAngle = INSTANCE_ANGLE);
        enblastEffect(fromActor = "instance", scale = "0.5", damage = WIND_STAFF_ENBLAST_DAMAGE,  toAngle = INSTANCE_ANGLE + "-16");
        enblastEffect(fromActor = "instance", scale = "0.5", damage = WIND_STAFF_ENBLAST_DAMAGE,  toAngle = INSTANCE_ANGLE + "-32");
        }
        if(weapon == "custom_iron_sword") {
            deltaHp(actorCode = "instance", value = "{HP}/4");
        }
    }
}

block clickIncreaseSTR = clickIncreaseDef("STR", "力量");
block clickIncreaseCON = clickIncreaseDef("CON", "體質");
block clickIncreaseAGI = clickIncreaseDef("AGI", "敏捷");
block clickIncreaseINT = clickIncreaseDef("INT", "智慧");
block clickIncreasePSY = clickIncreaseDef("PSY", "精神");

block releasePowerWindStaff = releasePowerDef("custom_wind_staff");
block releasePowerIronSword = releasePowerDef("custom_iron_sword");

// Block Implementation
block monsterDead {
    __repeat__ = -1;
    triggers {
        actorDead(actor = ActorMatch(id = "monster"));
    }
    actions {
        increment("Exp");
        // TODO: Popup text
    }
}

block monsterSpawn {
    __repeat__ = -1;
    __repeatInterval__ = 8000;
    checks {
        actorCount(actor = ActorMatch(id = "monster_deer", matchKind = "contain"), op = "<", value = "5");
    }
    actions {
        addActor(name = "鹿(60)", id = "monster_deer*", camp = "第三", 
                hp = 60, x = "random(21,40)", y = "random(0,19)", localVarname = "actor", 
                weapon1 = "fist", weapon2 = "fist", strength = "10", role = "鹿");
        actorFollow(actorId = "actor", type = "跟隨自己");
    }
}

block levelUp {
    __repeat__ = -1;
    checks {
        checkNumber(lhs = "{Exp}", op = ">=", rhs = "{ExpNeeded}");
    }
    actions {
        setInt("Exp", "{Exp}-{ExpNeeded}");
        increment("Level");
        increment("Point");
        increment("Point");
        setInt("HP", HP_FORMULA);
        actorAttributes(actorId = "instance", attr = "maxHp", value = "{HP}");
        setInt("ExpNeeded", "{Level}*2+4");
    }
}

block recover {
    __repeat__ = -1;
    __repeatInterval__ = 10000;
    checks {
        forEachActor(actor = ActorMatch(controller = "玩家"), varname = "instance");
    }
    actions {
        deltaHp(actorCode = "instance", value = RECOVERY_FORMULA);
    }
}

block pressZ {
    __repeat__ = -1;
    triggers {
        keyboardPressed(varName = "instance", key = "Z");
    }
    actions {
        actorTalk(actorId = "{instance.id}", text = STATUS_TEXT);
    }
}

block staffAttack {
    __repeat__ = -1;
    triggers {
        actorFire(weapon = "custom_wind_staff", varName = "instance");
    }
    actions {
        enblastEffect(fromActor = "instance", toAngle = INSTANCE_ANGLE, scale = "0.5", damage = WIND_STAFF_ENBLAST_DAMAGE);
    }
}

block swordAttack {
    __repeat__ = -1;
    triggers {
        actorHit(actor = ActorMatch(), actorVarName = "instance", hitter = ActorMatch(), hitterVarName = "attacker", weapon = "custom_iron_sword");
    }
    actions {
        deltaHp(actorCode = "instance", casterCode = "attacker", type = "流血受傷", value = "{STR}*3");
    }
}

block init {
    actions {
        setInt("STR", 0);
        setInt("CON", 0);
        setInt("AGI", 0);
        setInt("INT", 0);
        setInt("PSY", 0);
        setInt("Level", 1);
        setInt("Point", 5);
        setInt("Exp", 0);
        setInt("ExpNeeded", 6);
        setInt("HP", 100);
        setWeaponAbility(weapon = "小刀", operation = "無技能", level = 1);
        setWeaponAbility(weapon = "小刀", operation = "無技能", level = 2);
        setWeaponAbility(weapon = "custom_wind_staff");
        setWeaponAbility(weapon = "custom_iron_sword");
        addStuff(x = 4, y = 8, item = "custom_wind_staff");
        addStuff(x = 4, y = 8, item = "custom_wind_staff");
        addStuff(x = 4, y = 8, item = "custom_wind_staff");
        addStuff(x = 4, y = 8, item = "custom_wind_staff");
        addStuff(x = 4, y = 8, item = "custom_wind_staff");
        addMapSign(x = 4, y = 7, rotation = 90, text = "【法杖】\n武器傷害：5\n - 攻擊附帶傷害為【10+智慧+精神/2】的光彈\n技能：釋出5發光彈", showButtons = false);
        addMapSign(x = 6, y = 7, rotation = 90, text = "【鐵劍】\n武器傷害：【30+力量*3】\n技能：恢復四分之一的血量", showButtons = false);
        mapWarp(fromX = 19, fromY = 9, toX = 21, toY = 9, direction = "right");
        mapWarp(fromX = 19, fromY = 10, toX = 21, toY = 10, direction = "right");
        mapWarp(fromX = 21, fromY = 9, toX = 19, toY = 9, direction = "left");
        mapWarp(fromX = 21, fromY = 10, toX = 19, toY = 10, direction = "left");
        addStuff(x = 6, y = 8, item = "custom_iron_sword");
        addStuff(x = 6, y = 8, item = "custom_iron_sword");
        addStuff(x = 6, y = 8, item = "custom_iron_sword");
        addStuff(x = 6, y = 8, item = "custom_iron_sword");
        addStuff(x = 6, y = 8, item = "custom_iron_sword");
        addMapSign(x = 3, y = 17, rotation = 270, text = "【力量】\n消耗一點能力點，提升劍類傷害", buttonCode = "increaseSTR", buttonLabel = "確認");
        addMapSign(x = 5, y = 17, rotation = 270, text = "【體質】\n消耗一點能力點，增加十點血量上限及半點恢復力", buttonCode = "increaseCON", buttonLabel = "確認");
        addMapSign(x = 7, y = 17, rotation = 270, text = "【敏捷】\n消耗一點能力點，重量減少3%，並提升武器攻擊速度", buttonCode = "increaseAGI", buttonLabel = "確認");
        addMapSign(x = 9, y = 17, rotation = 270, text = "【智慧】\n消耗一點能力點，提升杖類傷害", buttonCode = "increaseINT", buttonLabel = "確認");
        addMapSign(x = 11, y = 17, rotation = 270, text = "【精神】\n消耗一點能力點，小量提升杖類傷害及提升一點恢復力", buttonCode = "increasePSY", buttonLabel = "確認");
    }
}